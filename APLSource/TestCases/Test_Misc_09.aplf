 R←Test_Misc_09(stopFlag batchFlag);⎕TRAP;body;parms;filename;html
⍝ Test for multiple lines of local vars definitions with a comment line between in them not left aligned in a tradfn.
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If 17>⊃(//)⎕VFI{⍵/⍨2>+\'.'=⍵}1⊃'#'⎕WG'APLVersion'
     R←T._NotApplicable
     :Return
 :EndIf

 R←T._Failed

 #.⎕SHADOW'TestHelp'
 'TestHelp'#.⎕NS''
 body←'r←Hello;a' '    ⍝ Now local vars' ';d;e' '⍝ Leading comment' 'r←1'
 #.TestHelp.⎕FX body

 parms←##.ADOC.ADOC.CreateBrowseDefaults
 parms.view←0
 filename←parms ##.ADOC.ADOC.Browse #.TestHelp
 html←⊃⎕NGET filename
 →T.GoToTidyUp 0=+/'<p>Leading comment</p>'⍷html

 R←T._OK

∆TidyUp:
 FilesAndDirs.DeleteFile filename
