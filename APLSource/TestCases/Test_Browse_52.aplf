 R←Test_Browse_52(stopFlag batchFlag);body;htmlFilename;buff;parms;⎕TRAP;myAdoc;expected
⍝ `Browse` an ordinary namespace with a function `ADOC_Doc` that carries the documentation; see also Test_Browse_69
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 ##.ADOC.RestoreDefaults

 #.⎕SHADOW'Temp'
 'Temp'#.⎕NS''

 body←,¨(⊂'ADOC_Doc'),(⊂'⍝ '),¨'My comment from line 1' '' '* This' '* That' '* More'
 #.Temp.⎕FX body
 #.Temp.⎕FX'r←Hello' 'r←''Word'''

 parms←##.ADOC.CreateBrowseDefaults
 parms.view←0
 myAdoc←⎕NEW ##.ADOC
 htmlFilename←myAdoc.Make #.Temp
 ##.ADOC.RestoreDefaults
 buff←⊃⎕NGET htmlFilename 1
 buff←⊃,/∆RemoveCSS buff
 buff↑⍨←('Reference</h2>'⍷buff)⍳1  ⍝ ADOC_Doc stuff must appear _before_ the reference part!
 expected←'<div class="header"><p>My comment from line 1</p><ul><li>This</li><li>That</li><li>More</li></ul>'
 →T.PassesIf 1=+/expected⍷∊buff

 R←T._OK
