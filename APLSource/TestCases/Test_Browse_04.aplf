 R←Test_Browse_04(stopFlag batchFlag);newdoc;buffer;flag;sep;⎕TRAP;code;ps;filename;html
⍝ `Browse` a class that has both a `Version` and `History` method.
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 ##.ADOC.ADOC.RestoreDefaults

 #.⎕SHADOW'Temp'
 'Temp'#.⎕NS''

 code←⊂':Class MyClass'
 code,←⊂'∇r←Version'
 code,←⊂':Access Public shared'
 code,←⊂'r←(⍕⎕THIS)''5.0.3'' ''2016-04-20'''
 code,←⊂'∇'
 code,←⊂'∇History'
 code,←⊂':Access Public shared'
 code,←⊂'⍝ 5.0.3:'
 code,←⊂'⍝ * `ADOC` remains unchanged but a new version of `MarkAPL` was integrated.'
 code,←⊂'⍝ 5.0.2:'
 code,←⊂'⍝ * `ADOC` remains unchanged but a new version of `MarkAPL` fixes an important problem.'
 code,←⊂'⍝ 5.0.1:'
 code,←⊂'⍝ * Handling `\\` sometimes failed. This happend when there were one or more trailing blanks after the `\\`.\\'
 code,←⊂'⍝   Now such blanks are removed.'
 code,←⊂'⍝ * Typo fixed'
 code,←⊂,'∇'
 code,←⊂':EndClass'

 #.Temp.⎕FIX code

 ps←##.ADOC.ADOC.CreateBrowseDefaults
 ps.view←0
 filename←ps ##.ADOC.ADOC.Browse #.Temp.MyClass
 html←⊃⎕NGET filename 1
 FilesAndDirs.DeleteFile filename
 html←∆ExtractBodyFromHTML 1↓⊃,/(⎕UCS 13),¨html
 ##.ADOC.ADOC.RestoreDefaults
 →T.PassesIf 1=+/'<ul'⍷html
 →T.PassesIf 0=+/'<ol'⍷html
 →T.PassesIf 4=+/'<li'⍷html

 R←T._OK
