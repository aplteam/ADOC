 R←Test_Browse_58(stopFlag batchFlag);⎕TRAP;code;ps;filename;html;ss;ind
⍝ `Browse` a scripted namespace that has a `Public` definition
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 ##.ADOC.ADOC.RestoreDefaults

 #.⎕SHADOW'Temp'
 'Temp'#.⎕NS''

 code←''
 code,←⊂':Namespace ScriptedNamespace'
 code,←⊂'⍝ This namespace script is used in the test for how ADOC processes namespaces script.'
 code,←⊂''
 code,←⊂'    ∇ r←Private1'
 code,←⊂'      r←''Private1'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ r←Private2 arg'
 code,←⊂'    ⍝ Comment 1.'
 code,←⊂'      r←''Private2'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ r←Public3(arg1 arg2)'
 code,←⊂'    ⍝ Comment 1.'
 code,←⊂'    ⍝ Comment 2.'
 code,←⊂'      r←''Public3'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ r←left Public4 arg'
 code,←⊂'    ⍝ Comment 1.\\'
 code,←⊂'    ⍝ Comment 2 on separate line.'
 code,←⊂'      r←''Public4'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ r←{left} Public5 arg'
 code,←⊂'    ⍝ Comment 1.\\'
 code,←⊂'    ⍝ Comment 2 on separate line.'
 code,←⊂'      r←''Public5'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ {r}←{left} Public6 arg'
 code,←⊂'    ⍝ Comment 1.\\'
 code,←⊂'    ⍝ Comment 2 on separate line.'
 code,←⊂'      r←''Public6'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ {(r1 r2)}←{left} Public7 arg'
 code,←⊂'    ⍝ Comment 1.\\'
 code,←⊂'    ⍝ Comment 2 on separate line.'
 code,←⊂'      r←''Public7'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ r←(fns Public8)arg'
 code,←⊂'      r←''Public8'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ r←leftArg(fns Public9)rightArg'
 code,←⊂'    ⍝ Operator comment 1.\\'
 code,←⊂'    ⍝ Operator comment 2 on separate line.'
 code,←⊂'      r←''Public9'''
 code,←⊂'    ∇'
 code,←⊂'    '
 code,←⊂'    ∇ r←leftArg(fns Private3)rightArg'
 code,←⊂'    ⍝ Operator comment 1.\\'
 code,←⊂'    ⍝ Operator comment 2 on separate line.'
 code,←⊂'      r←''Private3'''
 code,←⊂'    ∇'
 code,←⊂'    '
 code,←⊂'    ∇ r←Public'
 code,←⊂'     r←''Public1'' ''Public2'' ''Public3'' ''Public4'' ''Public5'' ''Public6'' ''Public7'' ''Public8'' ''Public9'' ''DoesNotExist'''
 code,←⊂'     r,←''Public7'' ''Public8'' ''Public9'' ''DoesNotExist2'''
 code,←⊂'    ∇'
 code,←⊂'    '
 code,←⊂':EndNamespace'

 #.Temp.⎕FIX code

 ps←##.ADOC.ADOC.CreateBrowseDefaults
 ps.view←0
 filename←ps ##.ADOC.ADOC.Browse #.Temp.ScriptedNamespace
 ##.ADOC.ADOC.RestoreDefaults
 html←⊃⎕NGET filename 1
 FilesAndDirs.DeleteFile filename
 html←1↓⊃,/(⎕UCS 10),¨∆RemoveCSS html
 ss←'<h5 class="aplname" id="ch0001:Public'
 ind←{⍵/⍳⍴,⍵}⊃(ss∘⍷¨⊂html)
 →T.PassesIf(3+⍳7)≡'Public'∘{{⍎⍵↑⍨⍵⍳'"'}(⍴⍺)↓⍵↓⍨1⍳⍨⍺⍷⍵}¨↓html[ind∘.+⍳3+⍴ss]
 ss←'<h5 class="aplname" id="ch0001:Private'
 →T.PassesIf 0∊⍴{⍵/⍳⍴,⍵}⊃(ss∘⍷¨⊂html)
 →T.PassesIf 1=⍴'<h4\b[^>]*>Shared Functions</h4>'⎕S 0⍠('Mode' 'M')('Greedy' 0)⊣html
 →T.PassesIf 1=⍴'<h4\b[^>]*>Shared Operators</h4>'⎕S 0⍠('Mode' 'M')('Greedy' 0)⊣html

 R←T._OK
