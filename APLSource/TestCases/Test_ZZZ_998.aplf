 R←Test_ZZZ_998(stopFlag batchFlag);⎕IO;⎕ML;⎕TRAP;report;buff;rf;candidate;lookFor1;lookFor2
⍝ Checks on two text vectors: "TODO" and "CHECK", embraced by "⍝"; never fails, just reports.
 ⎕IO←1 ⋄ ⎕ML←1
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._OK
 report←''
 rf←##.⎕THIS

 lookFor1←'⍝ C H E C K ⍝'~' '                                   ⍝ To avoid finding this function
 buff←rf T.FindSpecialString lookFor1
 buff←(buff[;1]≢¨⎕XSI[1])⌿buff  ⍝ remove caller
 :If 0≠≢buff
     report,←↓(⊂' ',lookFor1,' found:'),(⊂'   '),¨buff
 :EndIf
 lookFor2←'⍝ T O D O ⍝'~' '                                     ⍝ To avoid finding this function
 buff←rf T.FindSpecialString lookFor2
 buff←(buff[;1]≢¨⎕XSI[1])⌿buff  ⍝ remove caller
 candidate←9=⊃∘⎕NC¨buff[;1]
 buff←(candidate{⍺:⍵≡⍕⍎⍵ ⋄ 1}¨buff[;1])⌿buff   ⍝ remove references
 :If 0≠≢buff
     report←↓(⊂' ',lookFor2,' found:'),(⊂'   '),¨buff
     ⎕←('Checking for ',lookFor1,' and ',lookFor2,' '){⍵↑,'--- ',⍺,' ',⍵⍴'-'}80⌊⎕PW-1
     ⎕←↑report
     :If ~batchFlag
         0 0⍴⍞,0/⎕←'  To continue press <enter>'
     :EndIf
 :EndIf
⍝Done
