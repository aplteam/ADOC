 R←Test_Info_01(stopFlag batchFlag);newdoc;flag;buffer;sep;show;_;⎕TRAP
⍝ Exercise the "Info" method on an interface
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 sep←⎕UCS 13
 newdoc←sep(≠⊆⊢)##.ADOC.ADOC.Info testinterface
 show←batchFlag∘{⍺:⍬ ⋄ ⎕←⍵ ⋄ ⍬}
 ##.ADOC.ADOC.RestoreDefaults

 :If ≢/∆DropEmptyItems¨Correct_testinterface_002 newdoc
     _←show'   --- ',0⊃⎕SI
     flag←0
     :If 0≠≢buffer←↑{⍺/⍨~⍺∊⍵}/∆DropEmptyItems¨Correct_testinterface_002 newdoc
         _←show'  Should be contained but isn''t:'
         _←show buffer
         flag←1
     :EndIf
     :If 0≠≢buffer←↑{⍺/⍨~⍺∊⍵}/∆DropEmptyItems¨newdoc Correct_testinterface_002
         _←show'  Should not be contained but is:'
         _←show buffer
         flag←1
     :EndIf
     :If ~flag
         _←show'    Contents matches, sequence doesn''t!'
         R←T._OK ⍝ Because that's acceptable
     :EndIf
     →T.FailsIf flag∧stopFlag
 :EndIf
 R←T._OK
