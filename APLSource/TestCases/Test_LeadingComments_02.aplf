 R←Test_LeadingComments_02(stopFlag batchFlag);⎕TRAP;body;parms;filename;html
⍝ Test for multiple lines of local vars definitions including a comment line.
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If 17>⊃(//)⎕VFI{⍵/⍨2>+\'.'=⍵}1⊃'#'⎕WG'APLVersion'
     R←T._NotApplicable
     :Return
 :EndIf

 R←T._Failed

 #.⎕SHADOW'TestHelp'
 'TestHelp'#.⎕NS''
 body←':Class Foo' '∇r←Hello;a;b;c' '⍝ comment line' ';d;e' ';f;g' ':Access Public Shared' '⍝ This is a leading comment' 'a←1' 'b←2' 'r←a+b' '∇' ':EndClass'
 #.TestHelp.⎕FIX body

 parms←##.ADOC.ADOC.CreateBrowseDefaults
 parms.view←0
 filename←parms ##.ADOC.ADOC.Browse #.TestHelp.Foo
 html←⊃⎕NGET filename
 →T.GoToTidyUp 0=+/'<p>This is a leading comment</p>'⍷html

 R←T._OK

∆TidyUp:
 FilesAndDirs.DeleteFile filename
