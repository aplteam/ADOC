 R←Test_Browse_62(stopFlag batchFlag);⎕TRAP;buffer;ps;filename;html;allFns;public
⍝ `Browse` a non-scripted (ordinary) namespace. Has `Version`, `Copyright`, `History`.
 R←T._Failed

 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 ##.ADOC.ADOC.RestoreDefaults

 #.⎕SHADOW'Temp'
 'Temp'#.⎕NS''
 'OrdinaryNS'#.Temp.⎕CY ##.CiderConfig.HOME,'/Tests/TestData'

 ps←##.ADOC.ADOC.CreateBrowseDefaults
 ps.view←0
 filename←ps ##.ADOC.ADOC.Browse #.Temp.OrdinaryNS
 ##.ADOC.ADOC.RestoreDefaults
 html←⊃⎕NGET filename 1
 FilesAndDirs.DeleteFile filename
 html←1↓⊃,/(⎕UCS 10),¨∆RemoveCSS html
 allFns←' '~¨⍨#.Temp.OrdinaryNS.⎕NL-3
 public←#.Temp.OrdinaryNS.Public

 →T.PassesIf((⍴public)⍴6)≡+/¨public⍷¨⊂html
 →T.PassesIf'Copyright' 'History' 'Version'≡(∨/¨(allFns~public)⍷¨⊂html)/allFns~public

 R←T._OK
