 R←Test_Info_02(stopFlag batchFlag);newdoc;buffer;flag;MyADOC;sep;show;_;⎕TRAP
⍝ Create an ADOC instance and then execute the "Info" method in the instance
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 sep←⎕UCS 13
 show←batchFlag∘{⍺:⍬ ⋄ ⎕←⍵ ⋄ ⍬}
 ##.ADOC.RestoreDefaults

 MyADOC←⎕NEW ##.ADOC
 newdoc←MyADOC.Info BaseClass

 :If Correct_testclass_004≢newdoc
     _←show'   --- ',0⊃⎕SI
     flag←0
     :If ~0∊⍴buffer←↑{⍺/⍨~⍺∊⍵}/{⍵⊂⍨sep=⍵}¨Correct_testclass_004 newdoc
         _←show'  Should be contained but isn''t:'
         _←show buffer
         flag←1
     :EndIf
     :If ~0∊⍴buffer←↑{⍺/⍨~⍺∊⍵}/{⍵⊂⍨sep=⍵}¨newdoc Correct_testclass_004
         _←show'  Should not be contained but is:'
         _←show buffer
         flag←1
     :EndIf
     :If ~flag
         _←show'    Contents matches, sequence doesn''t!'
         R←T._OK                ⍝ Because that's acceptable
     :EndIf
     →T.FailsIf flag∧stopFlag
 :EndIf

 R←T._OK
