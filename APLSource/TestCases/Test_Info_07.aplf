 R←Test_Info_07(stopFlag batchFlag);⎕TRAP;code;buffer;expected
⍝ Exercise the "Info" method in verbose mode on a scripted namespace that has a `Public` definition
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 ##.ADOC.RestoreDefaults

 #.⎕SHADOW'Temp'
 'Temp'#.⎕NS''

 code←''
 code,←⊂':Namespace ScriptedNamespace'
 code,←⊂'⍝ This namespace script is used in the test for how ADOC processes namespaces script.'
 code,←⊂''
 code,←⊂'    ∇ r←Private1'
 code,←⊂'      r←''Private1'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ r←Private2 arg'
 code,←⊂'    ⍝ Comment 1.'
 code,←⊂'      r←''Private2'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ r←Public3(arg1 arg2)'
 code,←⊂'    ⍝ Comment 1.'
 code,←⊂'    ⍝ Comment 2.'
 code,←⊂'      r←''Public3'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ r←left Public4 arg'
 code,←⊂'    ⍝ Comment 1.\\'
 code,←⊂'    ⍝ Comment 2 on separate line.'
 code,←⊂'      r←''Public4'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ r←{left} Public5 arg'
 code,←⊂'    ⍝ Comment 1.\\'
 code,←⊂'    ⍝ Comment 2 on separate line.'
 code,←⊂'      r←''Public5'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ {r}←{left} Public6 arg'
 code,←⊂'    ⍝ Comment 1.\\'
 code,←⊂'    ⍝ Comment 2 on separate line.'
 code,←⊂'      r←''Public6'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ {(r1 r2)}←{left} Public7 arg'
 code,←⊂'    ⍝ Comment 1.\\'
 code,←⊂'    ⍝ Comment 2 on separate line.'
 code,←⊂'      r←''Public7'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ r←(fns Public8)arg'
 code,←⊂'      r←''Public8'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ r←leftArg(fns Public9)rightArg'
 code,←⊂'    ⍝ Operator comment 1.\\'
 code,←⊂'    ⍝ Operator comment 2 on separate line.'
 code,←⊂'      r←''Public9'''
 code,←⊂'    ∇'
 code,←⊂'    '
 code,←⊂'    ∇ r←leftArg(fns Private3)rightArg'
 code,←⊂'    ⍝ Operator comment 1.\\'
 code,←⊂'    ⍝ Operator comment 2 on separate line.'
 code,←⊂'      r←''Private3'''
 code,←⊂'    ∇'
 code,←⊂'    '
 code,←⊂'    ∇ r←Public'
 code,←⊂'     r←''Public1'' ''Public2'' ''Public3'' ''Public4'' ''Public5'' ''Public6'' ''Public7'' ''Public8'' ''Public9'''
 code,←⊂'     r,←''DoesNotExist'' ''Public7'' ''Public8'' ''Public9'' ''DoesNotExist2'''
 code,←⊂'    ∇'
 code,←⊂'    '
 code,←⊂':EndNamespace'

 #.Temp.⎕FIX code

 buffer←1 ##.ADOC.Info #.Temp.ScriptedNamespace
 buffer←(⎕UCS 13)APLTreeUtils2.Split buffer
 →T.PassesIf'*** ScriptedNamespace (Namespace) ***'≡0⊃buffer
 buffer←APLTreeUtils2.DMB 1↓buffer
 →T.PassesIf 1 1≡'Functions:' 'Operators:'∊buffer
 buffer~←'Functions:' 'Operators:' ''
 expected←'r ← Public3(arg1 arg2)' 'r ← left Public4 arg' 'r ← {left} Public5 arg' '{r} ← {left} Public6 arg' '{(r1 r2)} ← {left} Public7 arg' 'r ← (fns Public8)arg' 'r ← leftArg(fns Public9)rightArg'
 →T.PassesIf expected≡buffer

 R←T._OK
