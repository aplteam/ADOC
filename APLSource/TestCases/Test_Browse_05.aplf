 R←Test_Browse_05(stopFlag batchFlag);newdoc;buffer;flag;sep;⎕TRAP;code;filename;html;list;expected;ps
⍝ `Browse` a class that has a multi-line field definition
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 ##.ADOC.RestoreDefaults

 #.⎕SHADOW'Temp'
 'Temp'#.⎕NS''

 code←⊂':Class MyClass'
 code,←⊂':Field Public Instance ReadOnly abc←1 2 3 ⍝ First comment\\'
 code,←⊂'⍝ Second comment\\'
 code,←⊂'⍝ Third comment'
 code,←⊂'⍝ * This'
 code,←⊂'⍝ * That '
 code,←⊂'⍝ * More'
 code,←⊂'∇ Hello'
 code,←⊂':Access Public Shared'
 code,←⊂'r←(⍕⎕THIS)''5.0.3'' ''2016-04-20'''
 code,←⊂,'∇'
 code,←⊂':EndClass'

 #.Temp.⎕FIX code

 ps←##.ADOC.CreateBrowseDefaults
 ps.view←0
 filename←ps ##.ADOC.Browse #.Temp.MyClass
 ##.ADOC.RestoreDefaults
 html←##.APLTreeUtils.ReadUtf8File filename
 ##.FilesAndDirs.DeleteFile filename

 →T.PassesIf(⊂'<a name="abc" />')∊html
 html←(html⍳⊂'<a name="abc" />')↓html
 html←(html⍳⊂'</div>')↑html
 →T.PassesIf'<h5 class="aplname" id="ch0001:abc">abc</h5>'≡1⊃html
 →T.PassesIf'<p class="small">Instance</p>'≡2⊃html
 →T.PassesIf'<a name="Shared_Methods" />'≡4⊃html
 →T.PassesIf'<h4 id="ch0001:sharedmethods">Shared Methods</h4>'≡5⊃html
 list←(⎕UCS 13)##.APLTreeUtils.Split 3⊃html
 expected←'<div' '<p' '<p' '<p' '<ul' '<li' '<li' '<li' '</ul'
 →T.PassesIf expected≡{⍵↑⍨⌊/⍵⍳'> '}¨list

 R←T._OK
