 R←Test_Misc_04(stopFlag batchFlag);⎕TRAP;body;parms;filename;html
⍝ Test for function in a class that carries multiple lines of local vars definitions.
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If 17>⊃(//)⎕VFI{⍵/⍨2>+\'.'=⍵}1⊃'#'⎕WG'APLVersion'
     R←∆NotApplicable
     :Return
 :EndIf

 R←∆Failed

 #.⎕SHADOW'TestHelp'
 'TestHelp'#.⎕NS''
 body←':Class Foo' '∇r←Hello;a;b;c' ';d;e' ';f;g' ':Access Public Shared' '⍝ This is a leading comment' 'a←1' 'b←2' 'r←a+b' '∇' ':EndClass'
 #.TestHelp.⎕FIX body

 parms←##.ADOC.CreateBrowseDefaults
 parms.view←0
 filename←parms ##.ADOC.Browse #.TestHelp.Foo
 html←'flat'##.APLTreeUtils.ReadUtf8File filename
 →GoToTidyUp 0=+/'<p>This is a leading comment</p>'⍷html

 R←∆OK

∆TidyUp:
 ##.FilesAndDirs.DeleteFile filename
