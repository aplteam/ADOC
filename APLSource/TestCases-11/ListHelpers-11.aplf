 r←ListHelpers force;list;force;A
⍝ Lists all helpers available from the `Tester` class.
⍝ When called by a user pass a `0` as right argument to see all helpers that are actually available.
⍝ Specify a `1` in case you want to see all Helpers that **might** be available.
⍝ Helpers are usually established by calling the `EstablishHelpers' method.
⍝ The list includes helpers that won't be established in case the namespace hosting the test cases is scripted!
 force←⎕THIS≡{⎕ML←1 ⋄ ⊃⍵}(1↓⎕RSI),⊂''
 r←0 2⍴' '
 list←'Run' 'RunDebug' 'RunThese' 'RunBatchTests' 'RunBatchTestsInDebugMode' 'E' 'L' 'G' 'FailsIf' 'PassesIf'
 list,←'GoToTidyUp' 'RenameTestFnsTo' 'ListHelpers' '∆OK' '∆Failed' '∆NoBatchTest' '∆Inactive' '∆NoAcreTests'
 list,←'∆WindowsOnly' '∆LinuxOnly' '∆MacOnly' '∆LinuxOrMacOnly' '∆LinuxOrWindowsOnly'
 list,←'∆MacOrWindowsOnly' 'FindSpecialString' '∆NotApplicable'
 list←,¨list
 :If 'Tester.Helpers'≢{⍵↑⍨-+/∧\2>+\⌽'.'=⍵}⍕{⎕ML←1 ⋄ ⊃⍵}⎕RSI
     list/⍨←force∨0<{⎕ML←1 ⋄ ⊃⍵}∘⎕NC¨list   ⍝ List only those that are around
 :EndIf
 :If 9=#.⎕NC'APLTreeUtils'
     A←#.APLTreeUtils
 :ElseIf 9=⎕NC'APLTreeUtils'
     A←APLTreeUtils
 :ElseIf 9=##.⎕NC'APLTreeUtils'
     A←##.APLTreeUtils
 :ElseIf 9∊{⎕ML←1 ⋄ ⊃¨⍵}⎕RSI.⎕NC⊂'APLTreeUtils'
     A←({⎕ML←1 ⋄ ⊃⍵}(9={⎕ML←1 ⋄ ⊃ ⋄ ⍵}⎕RSI.⎕NC⊂'APLTreeUtils')/⎕RSI).APLTreeUtils
 :ElseIf 9∊{⎕ML←1 ⋄ ⊃¨⍵}⎕RSI.##.⎕NC⊂'APLTreeUtils'
     A←({⎕ML←1 ⋄ ⊃⍵}(9={⎕ML←1 ⋄ ⊃¨⍵}⎕RSI.##.⎕NC⊂'APLTreeUtils')/⎕RSI.##).APLTreeUtils
 :Else
     'Missing: APLTreeUtils'⎕SIGNAL 6
 :EndIf
 r←{⎕ML←1 ⋄ ↑⍵}{⍵(A.dlb{⍺⍺{⎕IO←1 ⋄ ⍵↓⍨¯1+⍵⍳'⍝'}⍺⍺ ⍵}⎕IO⊃(1↓⎕NR ⍵),⊂'')}¨list
