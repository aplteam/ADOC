 R←Test_Browse_54(stopFlag batchFlag);newdoc;buffer;flag;sep;⎕TRAP;code;html;filename;list;ps;ss;ind
⍝ `Browse` a scripted namespace that has only trad operators
 R←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 ##.ADOC.RestoreDefaults

 #.⎕SHADOW'Temp'
 'Temp'#.⎕NS''

 code←''
 code,←⊂':Namespace ScriptedNamespace'
 code,←⊂'⍝ This namespace script is used in the test for how ADOC processes namespace script.'
 code,←⊂'⍝ This one has no function `Public`, therefore all functions are listed.'
 code,←⊂''
 code,←⊂'    ∇ r←(fns Public1)arg'
 code,←⊂'      r←''Public1'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂'    ∇ r←leftArg(fns Public2)rightArg'
 code,←⊂'      r←''Public2'''
 code,←⊂'    ∇'
 code,←⊂''
 code,←⊂':EndNamespace'

 #.Temp.⎕FIX code

 ps←##.ADOC.CreateBrowseDefaults
 ps.view←0
 filename←ps ##.ADOC.Browse #.Temp.ScriptedNamespace
 ##.ADOC.RestoreDefaults
 html←#.APLTreeUtils.ReadUtf8File filename
 #.FilesAndDirs.DeleteFile filename
 html←1↓⊃,/(⎕UCS 10),¨∆RemoveCSS html
 ss←'<h5 class="aplname" id="ch0001:Public'
 ind←{⍵/⍳⍴,⍵}⊃(ss∘⍷¨⊂html)
 →PassesIf(1+⍳2)≡'Public'∘{{⍎⍵↑⍨⍵⍳'"'}(⍴⍺)↓⍵↓⍨1⍳⍨⍺⍷⍵}¨↓html[ind∘.+⍳3+⍴ss]

 R←∆OK
