 R←Test_Browse_62(stopFlag batchFlag);⎕TRAP;buffer;ps;filename;html;allFns;public
⍝ `Browse` a non-scripted (ordinary) namespace. Has `Version`, `Copyright`, `History`.
 R←∆Failed

 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 ##.ADOC.RestoreDefaults

 #.⎕SHADOW'Temp'
 'Temp'#.⎕NS''
 'OrdinaryNS'#.Temp.⎕CY'Tests\TestData'

 ps←##.ADOC.CreateBrowseDefaults
 ps.view←0
 filename←ps ##.ADOC.Browse #.Temp.OrdinaryNS
 ##.ADOC.RestoreDefaults
 html←#.APLTreeUtils.ReadUtf8File filename
 #.FilesAndDirs.DeleteFile filename
 html←1↓⊃,/(⎕UCS 10),¨∆RemoveCSS html
 allFns←' '~¨⍨#.Temp.OrdinaryNS.⎕NL-3
 public←#.Temp.OrdinaryNS.Public

 →PassesIf((⍴public)⍴4)≡+/¨public⍷¨⊂html
 →PassesIf'Copyright' 'History' 'Version'≡(∨/¨(allFns~public)⍷¨⊂html)/allFns~public

 R←∆OK
