 Make flag;M;C;path;zipFilename;res;cfg;parms
⍝ "Make" a new version of this project
 :If flag
     :If 0=⎕SE.⎕NC'MakeHelpers'
         {}⎕SE.Tatin.LoadPackages'MakeHelpers'⎕SE
     :EndIf
     M←⎕SE.MakeHelpers
     C←##.CiderConfig
     path←C.HOME,'/',C.CIDER.distributionFolder
     cfg←⎕JSON⍠('Dialect' 'JSON5')⊢⊃⎕NGET C.HOME,'/apl-package.json'
     M.FetchLaterUserCommand cfg C'[MyUCMDs]'
     cfg←M.EditVersion cfg
     M.RecreateFolder path
     parms←⎕SE.Tatin.CreateBuildParms C.HOME
     parms.targetPath←path
     parms.version←cfg.version
     parms.projectSpace←'#.ADOC'
     zipFilename←⎕SE.Tatin.BuildPackage parms
     ⎕←'*** New version build successfully:',M.CR,'   ',zipFilename
     :If 0<≢cfg.userCommandScript
     :AndIf 1 M.YesOrNo'Install new version in MyUCMDs?'
         {}M.##.FilesAndDirs.RmDirByForce M.GetMyUCMDsFolder cfg.name
         res←⎕SE.Tatin.InstallPackages zipFilename'[MyUCMDs]'
         ⎕←'  New version installed as user command in MyUCMDs/: ',res
     :EndIf
 :EndIf
 ⍝Done
